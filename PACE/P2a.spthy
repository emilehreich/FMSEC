theory P2a
begin

section{* 1.2 PACE - Mutual Authentication *}

/*
  Protocol:
    1. A -> B: x
    2. B -> A: y
    3. A -> B: [y]_k(A,B)
    4. B -> A: [x]_k(B,A)

*/

functions:
  mac/2, vfy/3, true/0

equations:
  vfy(k, mac(k, msg1), msg1) = true

// Key Infrastructure
rule Shared_Key:
  [ Fr(~sk)
  ]
--[ UniDKey($A, $B, ~sk)
  ]->
  [ !SKey($A, $B, ~sk)
  ]

rule Reveal_SKey_A:
    [ !SKey(A, B, k) 
    ]
  --[ Reveal(A) 
    ]->
    [ Out(k) 
    ]

rule Reveal_SKey_B:
    [ !SKey(A, B, k) 
    ]
  --[ Reveal(B) 
    ]->
    [ Out(k) 
    ]


// -- Protocol Rules --
// Thread Initialization

rule Init_A:
    [ Fr(~id)
    , !SKey(A, B, k1)
    , !SKey(B, A, k2)
    ]
  --[ Create(A, ~id, 'Alice', k1, k2)
    ]->
    [ St_A_1(A, ~id, B, k1, k2)
    ]

rule Init_B:
    [ Fr(~id)
    , !SKey(A, B, k1)
    , !SKey(B, A, k2)
    ]
  --[ Create(B, ~id, 'Bob', k1, k2)
    ]->
    [ St_B_1(B, ~id, A, k1, k2)
    ]

// Protocol Messages

rule A_1_Send:
    [ St_A_1(A, id, B, k1, k2)
    , Fr(~x)
    ]
  --[ Send(A, ~x)
    ]->
    [ St_A_2(A, id, B, k1, k2, ~x)
    , Out(~x)
    ]

rule B_1_Receive:
    [ St_B_1(B, id, A, k1, k2)
    , In(x)
    ]
  --[ Recv(B, x)
    ]->
    [ St_B_2(B, id, A, k1, k2, x)
    ]

rule B_2_Send:
    [ St_B_2(B, id, A, k1, k2, x)
    , Fr(~y)
    ]
  --[ Send(B, ~y)
    , Running(B, A, <'I', 'R', x, ~y>)
    ]->
    [ St_B_3(B, id, A, k1, k2, x, ~y)
    , Out(~y)
    ]

rule A_2_Receive:
    [ St_A_2(A, id, B, k1, k2, x)
    , In(y)
    ]
  --[ Recv(A, y)
    , Running(A, B, <'R', 'I', x, y>)
    ]->
    [ St_A_3(A, id, B, k1, k2, x, y)
    ]

rule A_3_Send:
    [ St_A_3(A, id, B, k1, k2, x, y)
    ]
  --[ Send(A, mac(k1, y))
    ]->
    [ St_A_4(A, id, B, k1, k2, x, y, mac(k1, y))
    , Out(mac(k1, y))
    ]

rule B_3_Receive:
    [ St_B_3(B, id, A, k1, k2, x, y)
    , In(mac(k1, y))
    ]
  --[ Recv(B, mac(k1, y))
    , Eq(vfy(k1, mac(k1, y), y), true)
    , Authenticated(A, B)
    , Commit(B, A, <'R', 'I', x, y>)
    ]->
    [ St_B_4(B, id, A, k1, k2, x, y, mac(k1, y))
    ]

rule B_4_Send:
    [ St_B_4(B, id, A, k1, k2, x, y, mac(k1, y))
    ]
  --[ Send(B, mac(k2, x))
    ]->
    [ St_B_5(B, id, A, k1, k2, x, y, mac(k1, y), mac(k2, x))
    , Out(mac(k2, x))
    ]

rule A_4_Receive:
    [ St_A_4(A, id, B, k1, k2, x, y, mac(k1, y))
    , In(mac(k2, x))
    ]
  --[ Recv(A, mac(k2, x))
      , Eq(vfy(k2, mac(k2, x), x), true)
      , Authenticated(B, A)
      , Commit(A, B, <'I', 'R', x, y>)
    ]->
    [ St_A_5(A, id, B, k1, k2, x, y, mac(k1, y), mac(k2, x))
    ]

// restrictions
restriction Equality:
	"All x y #i. Eq(x,y) @i ==> x = y"

restriction KeyUnidirection:
  "All A B k1 k2 #i. 
    UniDKey(A, B, k1)@i &
    UniDKey(B, A, k2)@i &
    not (A = B) ==> not (k1 = k2)"


// Security Properties
lemma Executability:
  exists-trace
    "Ex A B #i #j. 
       Authenticated(A, B)@i & Authenticated(B, A)@j & not (A = B) & not (#i = #j) & not (Ex A #j. Reveal(A) @j)"

lemma injectiveagreementINITIATOR:
  "All a b t #i.
      Commit(a,b,<'I','R',t>)@i &
      not ((Ex #r. Reveal(a)@r) | (Ex #r. Reveal(b)@r))
      ==> (Ex #j. Running(b,a,<'I','R',t>)@j
          & not (Ex a2 b2 #i2. Commit(a2,b2,<'I','R',t>)@i2
          & not (#i2 = #i)))"

lemma injectiveagreementRESPONDER:
  "All a b t #i.
      Commit(a,b,<'R','I',t>)@i &
      not ((Ex #r. Reveal(a)@r) | (Ex #r. Reveal(b)@r))
      ==> (Ex #j. Running(b,a,<'R','I',t>)@j
          & not (Ex a2 b2 #i2. Commit(a2,b2,<'R','I',t>)@i2
          & not (#i2 = #i)))"


end

